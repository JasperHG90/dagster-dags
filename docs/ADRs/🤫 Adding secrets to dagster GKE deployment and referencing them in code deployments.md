---
creation date: 2024-02-18 10:02
tags:
  - ADR
  - template
  - GKE
  - kubernetes
  - "#secrets"
  - secret-management
  - dagster
template: "[[üè∑ Templates/ADR template]]"
status: ‚úÖ Accepted
homepage:
---
## üìú Table of contents
---
```table-of-contents
```
## ‚úçÔ∏è Context
---
We want to use secrets in code locations so that we can interface with systems outside of GKE. For example, we want to send slack messages when a job fails or succeeds.

We need a structured approach to:
- Adding the secrets to the GKE namespace
- Referencing secrets in a code location
## ü§ù Decision
---
### Secrets added by the Terraform deployment
Some secrets are generated in the 'dagster-infra' Terraform deployment. These secrets are added as 'Opaque' Kubernetes secrets where appropriate. See example [here](https://github.com/JasperHG90/dagster-infra/blob/main/infra/secrets.tf#L22-L35):

```terraform
locals {
  environment_toupper = upper(var.environment)
  credentials = {
    "KUBERNETES_${local.environment_toupper}_SA_JSON_KEY_B64"     = google_service_account_key.kubernetes.private_key
    "CONTAINERADMIN_${local.environment_toupper}_SA_JSON_KEY_B64" = google_service_account_key.containeradmin.private_key
    "DATAREADWRITER_${local.environment_toupper}_HMAC_ACCESS_ID"  = google_storage_hmac_key.key.access_id
    "DATAREADWRITER_${local.environment_toupper}_HMAC_SECRET"     = google_storage_hmac_key.key.secret
  }
  credential_ids = {
    for key, value in local.credentials : key => google_secret_manager_secret.credentials[key].id
  }
  gcp_secret_manager_kubernetes_secret_names = toset([
    for secret in data.google_secret_manager_secrets.secrets.secrets : secret["secret_id"]
  ])
  kubernetes_secrets = merge(
    {
      "GCS_SECRET_ACCESS_KEY" = google_storage_hmac_key.key.secret
      "GCS_ACCESS_KEY_ID"     = google_storage_hmac_key.key.access_id
    },
    {
      for secret in data.google_secret_manager_secrets.secrets.secrets : secret["secret_id"] => data.google_secret_manager_secret_version.secrets[secret["secret_id"]].secret_data
    }
  )
}

resource "google_secret_manager_secret" "credentials" {
  for_each = local.credentials

  secret_id = each.key

  labels = {
    terraform = true
  }

  replication {
    auto {}
  }
}

resource "google_secret_manager_secret_version" "credentials" {
  for_each = local.credentials

  secret      = local.credential_ids[each.key]
  secret_data = each.value
}

resource "kubernetes_secret" "credentials" {
  for_each = local.kubernetes_secrets

  metadata {
    name      = replace(lower(each.key), "_", "-")
    namespace = "dagster-${var.environment}"
  }

  data = {
    "${each.key}" = each.value
  }

  type = "Opaque"
}
```
### Adding secrets not generated by Terraform
To add a secret and make it available to code locations, we have created a [GitHub Actions pipeline](https://github.com/JasperHG90/dagster-infra/blob/main/.github/workflows/add_secrets.yml). A user can add secrets to the GKE deployment by adding them to the 'dagster-infra' [Secrets and variables](https://github.com/JasperHG90/dagster-infra/settings/secrets/actions) GitHub settings.

Secrets must be prefixed with "DAGSTER_SECRET", and will be added to the GKE 'dagster-prd' namespace as an 'Opaque' secret. The name of the kubernetes secret will be in lower-case, and underscores are replaced with hyphens.

For example:
`DAGSTER_SECRET_SLACK_BOT_OAUTH_TOKEN` --> `dagster-secret-slack-bot-oauth-token`

Note that, when the secret is used in a python Dagster DAG definition, it should still be referred to using the name as defined in the GitHub secret settings. (see below for example).
### Using secrets in code locations
To use secrets in a code location in the 'dagster-dags' repository, a user must reference them in the helm 'values.yaml' file under "envSecrets". (see example [here](https://github.com/JasperHG90/dagster-infra/settings/secrets/actions)).

```yaml
# values.yaml (dagster-dags helm deployment values)
deployments:
	...
    envSecrets:
      - name: gcs-access-key-id
      - name: gcs-secret-access-key
      - name: dagster-secret-slack-bot-oauth-token
```

The secrets can be referenced in a DAG using the "os" library:

```python
import os
os.environ["DAGSTER_SECRET_SLACK_BOT_OAUTH_TOKEN"]
```
## ‚òùÔ∏èConsequences
---
- Users should not add secrets manually, but should add them using the GitHub Actions pipeline.
- Users don't have to add secrets manually. They can simply refer to the secrets they want and use the secrets in their own pipelines.
